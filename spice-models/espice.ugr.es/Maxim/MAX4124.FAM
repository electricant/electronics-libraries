* MAX4124 FAMILY MACROMODELS
* -------------------------
* FEATURES:
* 25MHz Gain Bandwidth Product
* 650uA Typical Supply Current
* Drive 250 Ohm Loads
* Stable at Gains >=10V/V
* Rail-to-Rail I/O
* Available in 5-Pin SOT23-5 (MAX4124)
*              8-Pin SO/uMAX (MAX4128)
*
* PART NUMBER    DESCRIPTION
* ___________    ______________________________
* MAX4124        Single 25MHz, Stable Av=10V/V
* MAX4128        Dual 25MHz, Stable Av=10V/V
*
*
*   ////////////// MAX4124 MACROMODEL //////////////////
*
*   ====>      REFER TO MAX4124 DATA SHEET       <====
*
* connections:        non-inverting input
*                     |   inverting input
*                     |   |   positive power-supply
*                     |   |   |   negative power-supply
*                     |   |   |   |   output
*                     |   |   |   |   |
* OUTPUT CONNECTS:    1   2   99  50  97
*
*
.SUBCKT MAX4124 1 2 99 50 97
****************INPUT STAGE**********************
I1 99 4 .5e-3
IBIASE 1 2 50NA
M1   5  2 4 99 MOSFET
R3   5 50 2828
M2   6  7 4 99 MOSFET
R4   6 50 2828
CI1  1  0 2P
CI2  2  0 2P
DP1  1 99 DA
DP2 50  1 DA
DP3  2 99 DB
DP4 50  2 DB
************************
************ GAIN, 1ST POLE, SLEW STAGE************
EH 99 98 99 50 0.5
G0  98  9 5 6 7.87e-3
VB 9 10 0V 
R0  98  9 127E3
C3 10 98 1.3e-10E5
*C3 10 98 1.3E-10
*** ***********
D107 10 99 Dp 
D103 10 100 Dp
D104 100 99 Dp
F100 100 99 POLY(1) VB -1.2MA 1
***
D108 50 10 Dp
D105 101 10 Dp
D106 50 101 Dp
F2 50 101 POLY(1) VB -1.2MA -1
*** ***********
D1 9 111 DP
D2 112 9 DP
V11 99 111 50MV
V12 112 50 50MV
************
I2 99 50 300U
VOS 7 1 0v
*CHANGE OFFSET VOLTAGE TO 0V FOR OPEN-LOOP, OTHERWISE VOS
*********COMMON-MODE ZERO STAGE********
******* POLE STAGE **********
G3 98 15 10 98 1E-3
R12 98 15 1E3
***************************
*********** change for second pole
C5 98 15 8P
* was 2.4pf
*  
*************OUTPUT STAGE****************
F5 99 38 VA8 1
D9 40 38 DX
D10 38 99 DX
VA7 99 40 0
G12 98 32 15 98 1E-6
*                ^ INSERT NODE FROM LAST STAGE HERE
R15 98 32 1E6
*D3 32 36 Dx
*D4 37 32 Dx
*V5 34 37  .3V
*V4 36 34  -.3V
*** V5,V4 SET ISC
R16 34 97 10
E1 99 33 99 32 1
VA8 33 34 0V
*RCON 35 97 15k
******************************************
.MODEL DA D(IS=100E-14 RS=.5K)
.MODEL DB D(IS=100E-14 RS=.5K)
.MODEL DX D(IS=100E-14)
.MODEL Dp D(N=0.05)
.MODEL MOSFET PMOS(VTO=0 KP=1.8E-3)
*VTO ESTABLISHES INPUT VOLT. RANGE  , was -1.7
*
.ENDS
*
*   ////////////// MAX4128 MACROMODEL //////////////////
*
*   ====>      REFER TO MAX4128 DATA SHEET       <====
*
* connections:        non-inverting input
*                     |   inverting input
*                     |   |   positive power-supply
*                     |   |   |   negative power-supply
*                     |   |   |   |   output
*                     |   |   |   |   |
* OUTPUT CONNECTS:    1   2   99  50  97
*
*
.SUBCKT MAX4128 1 2 99 50 97
****************INPUT STAGE**********************
I1 99 4 .5e-3
IBIASE 1 2 50NA
M1   5  2 4 99 MOSFET
R3   5 50 2828
M2   6  7 4 99 MOSFET
R4   6 50 2828
CI1  1  0 2P
CI2  2  0 2P
DP1  1 99 DA
DP2 50  1 DA
DP3  2 99 DB
DP4 50  2 DB
************************
************ GAIN, 1ST POLE, SLEW STAGE************
EH 99 98 99 50 0.5
G0  98  9 5 6 7.87e-3
VB 9 10 0V 
R0  98  9 127E3
C3 10 98 1.3e-10E5
*C3 10 98 1.3E-10
*** ***********
D107 10 99 Dp 
D103 10 100 Dp
D104 100 99 Dp
F100 100 99 POLY(1) VB -1.2MA 1
***
D108 50 10 Dp
D105 101 10 Dp
D106 50 101 Dp
F2 50 101 POLY(1) VB -1.2MA -1
*** ***********
D1 9 111 DP
D2 112 9 DP
V11 99 111 50MV
V12 112 50 50MV
************
I2 99 50 300U
VOS 7 1 0v
*CHANGE OFFSET VOLTAGE TO 0V FOR OPEN-LOOP, OTHERWISE VOS
*********COMMON-MODE ZERO STAGE********
******* POLE STAGE **********
G3 98 15 10 98 1E-3
R12 98 15 1E3
***************************
*********** change for second pole
C5 98 15 8P
* was 2.4pf
*  
*************OUTPUT STAGE****************
F5 99 38 VA8 1
D9 40 38 DX
D10 38 99 DX
VA7 99 40 0
G12 98 32 15 98 1E-6
*                ^ INSERT NODE FROM LAST STAGE HERE
R15 98 32 1E6
*D3 32 36 Dx
*D4 37 32 Dx
*V5 34 37  .3V
*V4 36 34  -.3V
*** V5,V4 SET ISC
R16 34 97 10
E1 99 33 99 32 1
VA8 33 34 0V
*RCON 35 97 15k
******************************************
.MODEL DA D(IS=100E-14 RS=.5K)
.MODEL DB D(IS=100E-14 RS=.5K)
.MODEL DX D(IS=100E-14)
.MODEL Dp D(N=0.05)
.MODEL MOSFET PMOS(VTO=0 KP=1.8E-3)
*VTO ESTABLISHES INPUT VOLT. RANGE  , was -1.7
*
.ENDS
