* MAX4112 FAMILY MACROMODELS
* -------------------------
* FEATURES:
* 500MHz -3dB Bandwidth
* 300MHz Full-Power Bandwidth
* 5mA Typical Supply Current
* 80mA Output Drive
* 1200V/uS Slew Rate
* Available in 8-Pin SO/uMAX 
*
* PART NUMBER    DESCRIPTION
* ___________    _____________________________
* MAX4112        500MHz, Low-Power Current-Mode 
*                Feedback Amplifiers  
*
*
*   ////////////// MAX4112 MACROMODEL //////////////////
*
*   ====>      REFER TO MAX4112 DATA SHEET       <====
*
* connections:      non-inverting input
*                   |   inverting input
*                   |   |   positive power supply
*                   |   |   |   negative power supply
*                   |   |   |   |   output
*                   |   |   |   |   |
*                   |   |   |   |   |
.SUBCKT  MAX4112     1   2  99  50  40
ISUPP 99 0 3.73MA
ISUPPN 50 0 -1.73MA
*** INPUT STAGE
I1 99  5 430U
I2  4 50 430U
Q1  50  3 5 QPN
Q2  99  3 4 QNN 
R1   5 6 633
C1   6 99 .63P
R2  4  7 633
C2   7 50 .63P
R3 99  8 633
V1  99 10 .3
RE1 10 30 130
D1  30  8 DX
R4 50  9 633
V2  11 50 .3
RE2 11 31 150
D2   9 31 DX
Q3   8  6 2 QNI
Q4   9  7 2 QPI
R6  1 99 20meg
R7  1 50 20meg
IB1 1 99 3.5UA
IB2 99  2 3.5UA
VOS 3 1 0V
CIN1 1  0 2.5P
CIN2 2  0 2.5P
*
**************SECOND STAGE**************
*
********************Isup  99 50 4.47M
R8  99 49 1meg
R9  49 50 1meg
V3  99 16 1.755
*pos swig
D3  15 16 DX
D4  17 15 DX
V4  17 50 1.755
*neg swing
EH  99 98 99 49 1
G1  98 15 POLY(2) 99 8 50 9 0 1.58E-3 1.58E-3
R5  98 15 2.372MEG
*** ***********************  1st pole here with r5, and c3
*C3 98 15 .25P
C3 98 15 .176P 
***************POLE STAGE*************** 
*
*Fp=250MHz
G2  98 20 15 49 1E-3
R14 98 20 1K
*C4  98 20 .692P
C4 98 20 .5P
*
***************POLE STAGE*************** 
*
*Fp=500 MHz
G3  98 21 20 49 1E-3
R15 98 21 1K
*C5  98 21 .03P
*
***************POLE STAGE*************** 
*
*Fp=250 MHz
G4  98 22 21 49 1E-3
R16 98 22 1K
*C6  98 22 .346P
*
***************POLE STAGE*************** 
*
*Fp=250 MHz
G5  98 23 22 49 1E-3
R17 98 23 1K
*C7  98 23 .246P
*
**************OUTPUT STAGE**************
*
F6  99 50 VA7 1
F5  99 35 VA8 1
D7  36 35 DX
VA7 99 36 0
D8  35 99 DX
E1  99 37 99 23 1
VA8 37 38 0
R35 38 40 12
V5  33 40 7V
D5  23 33 DX
V6  40 34 7V
D6  34 23 DX
CF1 40  2 1.8P
*** was 2.1pf
*
***************MODELS USED**************
*
.MODEL QNI NPN(IS=1E-14 BF=10E4 VAF=15 KF=6.7E-14)
.MODEL QPI PNP(IS=1E-14 BF=10E4 VAF=15 KF=6.7E-14)
.MODEL QNN NPN(IS=1E-14 BF=10E4 VAF=15 KF=4.13E-13)
.MODEL QPN PNP(IS=1E-14 BF=10E4 VAF=15 KF=4.13E-13)
.MODEL DX  D(IS=1E-15)
.MODEL DY  D(IS=1E-17)
.MODEL DN  D(KF=1.667E-9 AF=1 XTI=0 EG=.3)
*
.ENDS
