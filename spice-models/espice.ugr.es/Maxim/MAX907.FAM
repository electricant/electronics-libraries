* MAX907 FAMILY MACROMODELS
* -------------------------
* FEATURES:
* Fast, 40nS Propagation Delay (5mV overdrive)
* Wide Input Voltage Range Includes Ground
* Internal Hysteresis for Clean Switching
* Available in 8-Pin DIP/SO (Dual MAX907)
*             14-Pin DIP/SO (Quad MAX908)
*
* PART NUMBER    DESCRIPTION
* ___________    ___________________________________
* MAX907         Dual, int HYST, 40nS delay        
* MAX908         Quad, int HYST, 40nS delay
*
*
*   ////////////// MAX907 MACROMODEL //////////////////
*
*   ====>      REFER TO MAX907 DATA SHEET       <====
*
* connections:        non-inverting input, +IN
*                     |   inverting input, -IN
*                     |   |   positive power-supply, V+
*                     |   |   |   negative power-supply, V-
*                     |   |   |   |   output, OUT
*                     |   |   |   |   |
* NODE CONNECTIONS:   1   2   3   4   97
*
.subckt max907   1 2 3 4 97
*
  f101    3  9 v1 1
  Iee100   7  400 dc 100.0E-6
  q101    9  20  7 qin
  Q2    8  21  7 qin
  Q3    9  8  399 qmo
  Q4    8  8  399 qmi
  VMB 400 4 0V
  VPB 399 3 0V
***================
VIN1 2 23 .95
VIN2 1 25 .95
***
IPSUP 3 0 .6ma
INSUP 0 4 -40ua
***
EHYST 23 20 POLY(1) 0 60 0 1
VS2 21 25 0V
.model qin NPN(Is=800.0E-18 Bf=500)
.model qmi PNP(Is=800.0E-18 Bf=1002)
.model qmo PNP(Is=800.0E-18 Bf=1000 Cjc=1E-15 Tr=21.12E-9)
.MODEL PMOS PMOS 
*(VTO=-1.7 KP=1.8E-3)
  e1   10  4  3  9  2
  v1   10 11 dc 0
  q5    5 11  44 qoc
  vshift 44 4 0v
  *clamps output, Q5 collector load.
  R55 3 5 1meg
  DP5 5 3 DP
  DP6 4 5 DP
***============= Hysterisis section
GH 0 51 97 101 1E-6
ECM 101 0 3 4 0.5
RCM 101 0 10MEG
****==============COMPARATOR POINT FOR CREATING LOGIC OUTPUT, +-1, hi,lo.
RH1 3 51 1E11
RH2 4 51 1E11
DP1 51 52 DP
DP2 53 51 DP
VP1 52 0 1V
VP2 53 0 -1V
***=================
IHYST 55 0 -2E-9
*GENERATES 2MV OF HYST.
RREF 55 0 1E6
*LOGIC OUTPUT, NODE 60 ALTERS THE POLARITY, SO 55 SHOULD ALWAYS BE POS.
GMULT 60 0 POLY(2) 51 0 55 0 0 0  0 0 1E-6
RMULT 60 0 1E6
*================
*EH 3 98 3 4 0.5
VVIRTUAL 98 0 0V
F5 3 38 VA8 1
D9 40 38 DX
D10 38 3 DX
VA7 3 40 0
F6 3  4 VA7 1
G12 98 32 5 0 7.04E-3
R15 98 32 140
D3 36 41 DP
D4 42 37 DP
V4 37 34 0
V5 34 36 0
***V4,V5 SET ISC, V4 VOL, V5 VOH.
R16 41 35 150
R17 42 35 78
E11 3 33 3 32 1
VA8 33 34 0V
L 35 97 10NH
*========================
.model qoc NPN(Is=800.0E-18 Bf=103.5 Cjc=1E-15 Tf=5.545E-9 Tr=7.28E-9)
.MODEL DX  D(Is=800.0E-18)
.MODEL DP D(N=0.001)
*=======================
***== MODELS USED ==***
.MODEL DX2 D(IS=1E-15 n=0.001) 
*.MODEL DX D(IS=1E-15) 
.ends     
*
*   ////////////// MAX908 MACROMODEL //////////////////
*
*   ====>      REFER TO MAX908 DATA SHEET       <====
*
*
* connections:        non-inverting input, +IN
*                     |   inverting input, -IN
*                     |   |   positive power-supply, V+
*                     |   |   |   negative power-supply, V-
*                     |   |   |   |   output, OUT
*                     |   |   |   |   |
* NODE CONNECTIONS:   1   2   3   4   97
*
.subckt max908   1 2 3 4 97
*
  f101    3  9 v1 1
  Iee100   7  400 dc 100.0E-6
  q101    9  20  7 qin
  Q2    8  21  7 qin
  Q3    9  8  399 qmo
  Q4    8  8  399 qmi
  VMB 400 4 0V
  VPB 399 3 0V
***================
VIN1 2 23 .95
VIN2 1 25 .95
***
IPSUP 3 0 .6ma
INSUP 0 4 -40ua
***
EHYST 23 20 POLY(1) 0 60 0 1
VS2 21 25 0V
.model qin NPN(Is=800.0E-18 Bf=500)
.model qmi PNP(Is=800.0E-18 Bf=1002)
.model qmo PNP(Is=800.0E-18 Bf=1000 Cjc=1E-15 Tr=21.12E-9)
.MODEL PMOS PMOS 
*(VTO=-1.7 KP=1.8E-3)
  e1   10  4  3  9  2
  v1   10 11 dc 0
  q5    5 11  44 qoc
  vshift 44 4 0v
  *clamps output, Q5 collector load.
  R55 3 5 1meg
  DP5 5 3 DP
  DP6 4 5 DP
***============= Hysterisis section
GH 0 51 97 101 1E-6
ECM 101 0 3 4 0.5
RCM 101 0 10MEG
****==============COMPARATOR POINT FOR CREATING LOGIC OUTPUT, +-1, hi,lo.
RH1 3 51 1E11
RH2 4 51 1E11
DP1 51 52 DP
DP2 53 51 DP
VP1 52 0 1V
VP2 53 0 -1V
***=================
IHYST 55 0 -2E-9
*GENERATES 2MV OF HYST.
RREF 55 0 1E6
*LOGIC OUTPUT, NODE 60 ALTERS THE POLARITY, SO 55 SHOULD ALWAYS BE POS.
GMULT 60 0 POLY(2) 51 0 55 0 0 0  0 0 1E-6
RMULT 60 0 1E6
*================
*EH 3 98 3 4 0.5
VVIRTUAL 98 0 0V
F5 3 38 VA8 1
D9 40 38 DX
D10 38 3 DX
VA7 3 40 0
F6 3  4 VA7 1
G12 98 32 5 0 7.04E-3
R15 98 32 140
D3 36 41 DP
D4 42 37 DP
V4 37 34 0
V5 34 36 0
***V4,V5 SET ISC, V4 VOL, V5 VOH.
R16 41 35 150
R17 42 35 78
E11 3 33 3 32 1
VA8 33 34 0V
L 35 97 10NH
*========================
.model qoc NPN(Is=800.0E-18 Bf=103.5 Cjc=1E-15 Tf=5.545E-9 Tr=7.28E-9)
.MODEL DX  D(Is=800.0E-18)
.MODEL DP D(N=0.001)
*=======================
***== MODELS USED ==***
.MODEL DX2 D(IS=1E-15 n=0.001) 
*.MODEL DX D(IS=1E-15) 
.ends     
