* OP-21H SPICE Macro-model                  9/91, Rev. A
*                                           JCB / PMI
*
* This version of the OP-21 model simulates the worst case
* parameters of the 'H' grade.  The worst case parameters
* used correspond to those in the data sheet.
*
*
* Copyright 1991 by Analog Devices, Inc.
*
* The information on this diskette is protected under the
* United States copyright laws.  Analog Devices, Inc.("ADI")
* hereby grants users of these macro-models hereto referred
* to as licensee, a nonexclusive, nontransferable license to
* use these macro-models as long as the licensee abides by
* the terms of this agreement.  Before using the macro-
* models the licensee should read this license.  If the
* licensee does not accept these terms, this diskette should
* be returned to ADI within 30 days.
*
* The licensee may not sell, loan, rent, lease, or license
* the macro-models, in whole, in part, or in modified form,
* to anyone outside the licensee's company.  The licensee
* may modify these macro-models to suit his specific
* applications, and the licensee may make copies of this 
* diskette or macromodels for use within his company only.
*
* These macro-models are provided "AS IS, WHERE IS, AND WITH
* NO WARRANTY OF ANY KIND EITHER EXPRESSED OR IMPLIED, 
* INCLUDING BUT NOT LIMITED TO ANY IMPLIED WARRANTIES OF
* MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE."
* In no event will ADI be liable for special, collateral,
* incidental, or consequential damages in connection with or
* arising out of the use of these macro-models.  ADI
* reserves the right to make changes to the products and the
* macro-models without prior notice.
*
*
* Node assignments
*                non-inverting input
*                | inverting input
*                | | positive supply
*                | | |  negative supply
*                | | |  |  output
*                | | |  |  |
.SUBCKT OP-21H   1 2 99 50 31
*
* INPUT STAGE & POLE AT 2.8 MEGHZ
*
R1    2  3     5E11
R2    1  3     5E11
R3    5 50     6.29E3
R4    6 50     6.29E3
R5    4  7     1.13E3
R6    4  8     1.13E3
CIN   1  2     2E-12
C2    5  6     4.52E-12
I1    99 4     10E-6
IOS   1  2     3E-9
EOS   9  1     POLY(1)  24 27  500E-6  1
Q1    5  2  7  QX
Q2    6  9  8  QX
EREF  98 0    27  0  1
*
* GAIN STAGE & DOMINANT POLE AT 1.26 HZ
*
R7   10 98     3.146E9
G1   98 10     5  6  158.95E-6
C3   10 98     40E-12
D1   10 11     DX
D2   12 10     DX
V1   99 11     1.47
V2   12 50     1.67
*
* POLE AT 3.0 MEGHZ
*
R10  16 98     1E6
C5   16 98     53.1E-15
G3   98 16     10 27  1E-6
*
* POLE AT 3.0 MEGHZ
*
R16  17 98     1E6
C7   17 98     53.1E-15
G10  98 17     16 27  1E-6
*
* COMMON-MODE GAIN NETWORK WITH ZERO AT 200 HZ
*
R8   24 98     1
R9   23 24     1E6
C4   23 24     0.796E-9
E4   98 23     3  27  63.1
*
* POLE AT 3.0 MEGHZ
*
R15  25 98     1E6
C6   25 98     53.1E-15
G9   98 25     17 27  1E-6
*
* OUTPUT STAGE
*
ISY  99 50     266E-6
R11  27 99     104E3
R12  27 50     104E3
R13  30 99     450
R14  30 50     450
L1   30 31     1E-8
G4   28 50     25 30  2.222E-3
G5   29 50     30 25  2.222E-3
G6   30 99     99 25  2.222E-3
G7   50 30     25 50  2.222E-3
D5   99 28     DX
D6   99 29     DX
D7   50 28     DY
D8   50 29     DY
D9   25 33     DX
V3   33 30     0.75
D10  32 25     DX
V4   30 32     0.3
*
* MODELS USED
*
.MODEL QX PNP(BF=33.333)
.MODEL DX   D(IS=1E-15)
.MODEL DY   D(IS=1E-15 BV=50)
.ENDS OP-21H
